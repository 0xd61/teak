bool debug #option;
bool runTests #option;

void RunTests(str executable) {
	// TODO Timeout.
		
	FileWriteAll("tests_log.txt", "!! running tests...\n");
	int success = 0;
	int total = 0;

	for str file in DirectoryEnumerateRecursively("tests"):assert() {
		bool expected;
		str noRun = "--output-overview" if StringContains(file, ".norun") else "";

		if StringEndsWith(file, ".err.esh") {
			FileAppend("tests_log.txt", "!! expecting error from '%file%'...\n");
			expected = false;
		} else if StringEndsWith(file, ".esh") {
			FileAppend("tests_log.txt", "!! expecting success from '%file%'...\n");
			expected = true;
		} else {
			continue;
		}

		if SystemShellExecute("%executable% --want-completion-confirmation %noRun% tests/%file% 2>> tests_log.txt") == expected
				&& PathDelete("completion_confirmation.txt"):success() { 
			success += 1; 
		} else { 
			FileAppend("tests_log.txt", "!! test failed\n");
			LogError("The test failed.");
		}

		total += 1;
	}

	LogInfo("%success%/%total% tests succeeded.");
}

void Start() { 
	bool okay;
	str executable = "./esh";

	if SystemGetHostName() == "Windows" {
		str optimizeFlags = "" if debug else "/O2";
		assert SystemShellExecute("cl.exe /Zi esh.c /Wall %optimizeFlags% /link /OUT:new_esh.exe");
		okay = SystemShellExecute("new_esh examples\\hello_world.esh");
		executable = "new_esh";
	} else {
		str optimizeFlags = "-fsanitize=address" if debug else "-O2";
		assert SystemShellExecute("gcc -o new_esh esh.c -g -Wall -Wextra %optimizeFlags% -pthread -ldl -rdynamic"); 
		okay = SystemShellExecute("./new_esh examples/hello_world.esh");
	}

	if okay {
		if SystemGetHostName() == "Windows" {
			LogInfo("New build looks okay, please run \"move new_esh.exe esh.exe\".");
		} else {
			LogInfo("New build looks okay, replacing existing executable...");
			PathMove("new_esh", "esh");
		}
	} else {
		LogError("New build looks bad.");
		assert false;
	}

	if runTests {
		RunTests(executable);
	}
}
