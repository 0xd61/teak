bool debug #option;
bool runTests #option;

void RunTests() {
	// TODO Timeout.
		
	FileWriteAll("tests_log.txt", "!! running tests...\n");
	int success = 0;
	int total = 0;

	for str file in DirectoryEnumerate("tests"):assert() {
		bool expected;

		if StringEndsWith(file, ".err.esh") {
			FileAppend("tests_log.txt", "!! expecting error from '%file%'...\n");
			expected = false;
		} else if StringEndsWith(file, ".esh") {
			FileAppend("tests_log.txt", "!! expecting success from '%file%'...\n");
			expected = true;
		} else {
			continue;
		}

		if SystemShellExecute("./esh tests/%file% 2>> tests_log.txt") == expected { 
			success += 1; 
		} else { 
			FileAppend("tests_log.txt", "!! test failed\n");
		}

		total += 1;
	}

	LogInfo("%success%/%total% tests succeeded.");
}

void Start() { 
	bool okay;
	str optimizeFlags = "";

	if SystemGetHostName() == "Windows" {
		if !debug optimizeFlags = "/O2";
		assert SystemShellExecute("cl.exe esh.c /Wall %optimizeFlags% /link /OUT:new_esh.exe"); 
		okay = SystemShellExecute("new_esh examples\\hello_world.esh");
	} else {
		if !debug optimizeFlags = "-O2";
		assert SystemShellExecute("gcc -o new_esh esh.c -g -Wall -Wextra %optimizeFlags% -pthread -ldl -rdynamic"); 
		okay = SystemShellExecute("./new_esh examples/hello_world.esh");
	}

	if okay {
		LogInfo("New build looks okay, replacing existing executable...");
		PathMove("new_esh", "esh");
	} else {
		LogError("New build looks bad.");
		assert false;
	}

	if runTests {
		RunTests();
	}
}
